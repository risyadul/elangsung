// Top-level build file where you can add configuration options common to all sub-projects/modules.
def versionBuild = System.getenv("BUILD_NUMBER")?.toInteger() ?: 0

ext {
    appVersion = [
            major: 1,
            minor: 0,
            patch: 0
    ]
    buildVersionCode = { versionNameSuffix ->
        if (versionNameSuffix == null) {
            type = 9
        } else if (versionNameSuffix == "mock") {
            type = 0
        } else if (versionNameSuffix == "dev") {
            type = 1
        } else if (versionNameSuffix == "stage") {
            type = 2
        } else {
            throw new GradleException("Unknown type = " + versionNameSuffix)
        }
        return appVersion.major * 100000 + appVersion.minor * 1000 + appVersion.patch * 10 + type
    }
    buildVersionName = { versionNameSuffix ->
        if (versionNameSuffix != null) versionName = appVersion.major + "." + appVersion.minor + "." + appVersion.patch + "-" + versionNameSuffix + " (" + versionBuild + ")"
        else versionName = appVersion.major + "." + appVersion.minor + "." + appVersion.patch
        return versionName
    }
}


buildscript {

    ext.room_version = '2.4.1'
    ext.navigation_version = '2.3.5'
    ext.paging_version = '3.1.0'
    ext.multidex_version = "2.0.1"
    ext.viewpager2_version = "1.0.0"
    ext.recyclerview_version = "1.2.1"
    ext.rxjava_version = '2.2.19'
    ext.rxandroid_version = '2.1.1'
    ext.rxbinding_version = '2.1.1'
    ext.dagger_version = '2.38.1'
    ext.retrofit_version = '2.9.0'
    ext.retrofit_rxjava2_version = '2.9.0'
    ext.okhttp_logging_version = '4.9.2'
    ext.glide_version = '4.11.0'
    ext.glide_transformation_version = '3.1.1'
    ext.shimmer_version = '0.5.0'
    ext.chuck_version = '3.5.2'
    ext.swipe_refresh_layout_version = '1.1.0'
    ext.build_gradle_version = '7.0.0'
    ext.kotlin_version = '1.6.10'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$build_gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
